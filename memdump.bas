10 LET A=0
20 LET B=256
30 GOSUB 3000
100 STOP

3000 REM PRINT a block of memory (A=start, B=len)
3001 LET C = A AND &HFFF0
3002 LET B = B + A - C
3003 LET A = C
3010 FOR ADDRBK=A TO A+B STEP 16
3011 IF ADDRBK<&H10 THEN PRINT "0";
3012 IF ADDRBK<&H100 THEN PRINT "0";
3013 IF ADDRBK<&H1000 THEN PRINT "0";
3014 IF ADDRBK<&H10000 THEN PRINT "0";
3015 PRINT HEX$(ADDRBK);
3016 PRINT ": ";
3020 FOR XDR=ADDRBK TO ADDRBK + &HF
3030 LET DT = PEEK(XDR)
3040 IF DT<&H10 THEN PRINT "0";
3050 PRINT HEX$(DT);
3060 PRINT " ";
3065 IF (XDR AND &HF) = &H7 THEN PRINT " ";
3070 NEXT XDR
3080 PRINT "  ";
3090 FOR XDR=ADDRBK TO ADDRBK + &HF
3100 LET DT = PEEK(XDR)
3110 IF DT < 32 THEN LET DT = &H2E
3120 IF DT > 126 THEN LET DT = &H2E
3130 PRINT CHR$(DT);
3140 NEXT XDR
3150 PRINT " "
3160 NEXT ADDRBK
3170 LET A = A + B
3180 RETURN
